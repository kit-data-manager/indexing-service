plugins { 
    id "org.springframework.boot" version "3.1.5"
    id "io.spring.dependency-management" version "1.1.4" 
    id "io.freefair.lombok" version "8.4"
    id "io.freefair.maven-publish-java" version "8.4"
    id "org.owasp.dependencycheck" version "8.4.2"
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'net.researchgate.release' version '3.0.2'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id 'java'
    id 'jacoco'
}

ext {
  // versions of dependencies
  javersVersion     = '7.3.6'
  springDocVersion  = '2.2.0'
}

description = 'Generic indexing service supporting different mapping implementations.'
group = 'edu.kit.datamanager'

println "Running gradle version: $gradle.gradleVersion"
println "Building ${name} version: ${version}"
println "JDK version: ${JavaVersion.current()}"

repositories {
    mavenLocal() 
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
 
configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

if (System.getProperty('profile') == 'minimal') {
    println 'Using minimal profile for building ' + project.getName()
    apply from: 'gradle/profile-minimal.gradle'   
} else {
    println 'Using default profile executing all tests for building ' + project.getName()
    apply from: 'gradle/profile-complete.gradle'
}

dependencies {
    // Spring
    implementation 'org.springframework:spring-messaging:6.0.2'
    implementation 'org.springframework.cloud:spring-cloud-gateway-mvc:4.0.6'

    // Spring Boot
    // boot starter
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-amqp"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation 'org.springframework.data:spring-data-elasticsearch:5.2.0'
   
    // springdoc
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-common:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:${springDocVersion}"

    // driver for postgres
    implementation "org.postgresql:postgresql:42.7.1"
    //driver for h2
    implementation "com.h2database:h2:2.2.224"
    
    //apache
    implementation "org.apache.tika:tika-core:2.9.1"
    implementation "commons-codec:commons-codec:1.16.0"
  
    implementation "com.github.jknack:handlebars:4.3.1"
    implementation "org.json:json:20231013"
  
    // javers
    implementation "org.javers:javers-spring-boot-starter-sql:${javersVersion}"
    implementation "org.javers:javers-core:${javersVersion}"

    // datamanager
    implementation "edu.kit.datamanager:generic-message-consumer:1.0.0"
    implementation "edu.kit.datamanager:repo-core:1.2.1"
    implementation "edu.kit.datamanager:service-base:1.2.0"

    // actuator
    implementation 'de.codecentric:spring-boot-admin-starter-client:3.0.4'

    runtimeOnly    'org.apache.httpcomponents:httpclient:4.5.14'

    // Additional libraries for tests
    testImplementation "com.google.guava:guava:32.1.3-jre"
   
    //Java 11 Support 
    testImplementation "org.mockito:mockito-core:5.7.0"
    testImplementation "junit:junit:4.13.2"

     // boot starter
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:3.0.0'
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.springframework:spring-test"
}

jar {
    // disable plain jar file
    enabled = false
}

springBoot {    
    buildInfo()
}

bootJar {
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE 
    launchScript()
}

jacoco {
    toolVersion = "0.8.10"
}

// task for printing project name.
task printProjectName {
    doLast {
        println "${project.name}"
    }
}
